#compdef layman

# layman-1.1.1

local arguments actions_args actions global_opts add_actions

_layman_remote_overlays() {
  local layman_list_overlays
  layman_list_overlays=(${(S)${${(f)"$(layman -k -N -L 2>/dev/null)"}//\[*}#\* })
  compadd "$@" ${(kv)=layman_list_overlays} "ALL"
}

_layman_local_overlays() {
  local layman_list_overlays
  layman_list_overlays=(${(S)${${(f)"$(layman -N -l)"}//\[*}#\* })
  compadd "$@" ${(kv)=layman_list_overlays} "ALL"
}

actions=(
  --delete -d
  --sync -s
  --info -i
  --sync-all -S
  --list -L
  --list-local -l
  --fetch -f
)

global_opts=(
  '(--config -c)'{-c,--config}'[Select an alternative configuration file]:configuration file:_files'
  '(--overlays -o)'{-o,--overlays}'[Specifies the location of additional overlay lists]:overlay url:_urls'
  '(--nofetch -n)'{-n,--nofetch}'[Prevents from automatically fetching the remote lists of overlays]'
  '(--nocheck -k)'{-k,--nocheck}'[Prevents from checking the remote lists of overlays for complete overlay definitions]'
  '(--quiet -q)'{-q,--quiet}'[Makes layman completely quiet]'
  '(--verbose -v)'{-v,--verbose}'[Verbose output]'
  '(--quietness -Q)'{-Q,--quietness}'[Makes layman less verbose]:level:({0..4})'
  '(--nocolor -N)'{-N,--nocolor}'[Remove color codes from the output]'
)

add_actions=(
  -p --priority
  --add -a
)

actions_args=(
  "($actions --add -a)"{--add,-a}'[Add the given overlay to your installed overlays]:overlay:_layman_remote_overlays'
  "($actions --priority -p)"{--priority,-p}'[It will modify the priority of the added overlay]:priority:({0..100})'
  "($actions $add_actions)"{--delete,-d}'[Delete the given overlay from your installed overlays]:overlay:_layman_local_overlays'
  "($actions $add_actions)"{--sync,-s}'[Update the specified overlay]:overlay:_layman_local_overlays'
  "($actions $add_actions)"{--info,-i}'[Display all available information about the specified overlay]:overlay:_layman_remote_overlays'
  "($actions $add_actions)"{--sync-all,-S}'[Update all overlays]'
  "($actions $add_actions)"{--list,-L}'[List the contents of the remote list]'
  "($actions $add_actions)"{--list-local,-l}'[List the locally installed overlays]'
  "($actions $add_actions)"{--fetch,-f}'[Fetches the remote list of overlays]'
)

_arguments $global_opts[@] $actions_args[@]

# vim: set et sw=2 ts=2 ft=zsh:
